/*
Clafer model Allocation Case Study - ISC2 project
Authors : Jerome Le Noir (TRT), Stephen Creff (IRT)
*/

abstract ProcFunction
 execTime -> integer
  [this.ref >= 0]
 deployedFTo -> Processor
  [parent in this.allocatedTo]
 xor functionKind
  FPGA
   [parent.parent.deployedFTo.processorKind.FPGA]
  GPP
   [parent.parent.deployedFTo.processorKind.GPP]
  GPU
   [parent.parent.deployedFTo.processorKind.GPU] 
    
abstract Processor
 maxloadusage -> integer
  [this.ref >= 0]
 totalExecTimebyP -> integer
  [this.ref >= 0] 
 [totalExecTimebyP < maxloadusage]
 
 allocatedTo -> ProcFunction 0..17
  [this.deployedFTo = parent]

 hostedBy -> Board
  [parent in this.localProcessors]
 xor processorKind
  FPGA
   [parent.parent.maxloadusage = 6650]
   [parent.parent.totalExecTimebyP = sum allocatedTo.execTime] 
  GPP
   [parent.parent.maxloadusage = 12000]
   [parent.parent.totalExecTimebyP = sum allocatedTo.execTime] 
  GPU
   [parent.parent.maxloadusage = 5600]
   [parent.parent.totalExecTimebyP = sum allocatedTo.execTime]
  
abstract Board
 gpp : Processor
  [GPP]
 gpu : Processor
  [GPU]
 fpga : Processor ?
  [FPGA]
 localProcessors -> Processor *
  [this.hostedBy = parent]
 [localProcessors = this.gpp, this.gpu, this.fpga]

 
abstract SubsystemFunctionalArchitecture
 A1 : ProcFunction
  [GPP]
  [execTime = 2500]
 A2 : ProcFunction
  [execTime = 1500]
  [GPP]
 B1 : ProcFunction
  [FPGA]
  [execTime = 2870]
  
 C1 : ProcFunction
  [FPGA]
  [execTime = 1400]

 C20 : ProcFunction
  [GPU]
  [execTime = 4300]
  
 C21 : ProcFunction
  [GPU]
  [execTime = 5500]
  
 C30 : ProcFunction
  [GPP]
  [execTime = 4350]
  
 C31 : ProcFunction
  [GPP]
  [execTime = 6100]

 C4 : ProcFunction
  [GPP]
  [execTime = 100]
    
 E2 : ProcFunction
  [GPP]
  [execTime = 1000]
  
 E1 : ProcFunction
  [FPGA]
  [execTime = 1250]

 D1 : ProcFunction
  [FPGA]
  [execTime = 2000]

 D2 : ProcFunction
  [GPP]
  [execTime = 900]  
  
 F1 : ProcFunction
  [GPP]
  [execTime = 8000]  
 
 C5 : ProcFunction
  [GPP]
  [execTime = 7000] 
 
 F2 : ProcFunction
  [GPP]
  [execTime = 8000] 

 F3 : ProcFunction
  [GPP]
  [execTime = 5000]     

abstract HardwareArchitecture
 board0 : Board
  [no fpga] 
 board1 : Board
  [no fpga]
 board2 : Board
  [fpga]
 board3 : Board 
  [fpga]
 
Architecture 
 myArchitecture : HardwareArchitecture 
 mydataFlowArchitecture : SubsystemFunctionalArchitecture 

