/*
Clafer model Allocation Case Study - ISC2 project
Authors : Jerome Le Noir (TRT), Stephen Creff (IRT)
*/

abstract ProcFunction
 execTime -> integer
  [this.ref >= 0]
 deployedFTo -> Processor
  [parent in this.allocatedTo]

abstract Processor
 maxloadusage -> integer
  [this.ref >= 0]
 totalExecTimebyP -> integer
  [this.ref >= 0] 
 [totalExecTimebyP < maxloadusage]
 
 allocatedTo -> ProcFunction 0..17
  [this.deployedFTo = parent]

 hostedBy -> Board
  [parent in this.localProcessors]
     
abstract FPGAProcessor : Processor
 [maxloadusage = 6650]
 [allocatedTo in mydataFlowArchitecture.localFPGAFunctions]
 [totalExecTimebyP = sum allocatedTo.execTime] 
 
abstract GPPProcessor : Processor
 [maxloadusage = 12000] 
 [allocatedTo in mydataFlowArchitecture.localGPPFunctions]
 [totalExecTimebyP = sum allocatedTo.execTime] 

abstract GPUProcessor : Processor
 [maxloadusage = 5600]
 [allocatedTo in mydataFlowArchitecture.localGPUFunctions]
 [totalExecTimebyP = sum allocatedTo.execTime] 

abstract Board
 gpp : GPPProcessor
 gpu : GPUProcessor
 fpga : FPGAProcessor ? 
 localProcessors -> Processor *
  [this.hostedBy = parent]
 [localProcessors = this.gpp, this.gpu, this.fpga]

abstract SubsystemFunctionalArchitecture
 A1 : ProcFunction
  [execTime = 2500]
 A2 : ProcFunction
  [execTime = 1500]
 B1 : ProcFunction
  [execTime = 2870]
  
 C1 : ProcFunction
  [execTime = 1400]

 C20 : ProcFunction
  [execTime = 4300]
  
 C21 : ProcFunction
  [execTime = 5500]
  
 C30 : ProcFunction
  [execTime = 4350]
  
 C31 : ProcFunction
  [execTime = 6100]

 C4 : ProcFunction
  [execTime = 100]
    
 E2 : ProcFunction
  [execTime = 1000]
  
 E1 : ProcFunction
  [execTime = 1250]

 D1 : ProcFunction
  [execTime = 2000]

 D2 : ProcFunction
  [execTime = 900]  
  
 F1 : ProcFunction
  [execTime = 8000]  
 
 C5 : ProcFunction
  [execTime = 7000] 
 
 F2 : ProcFunction
  [execTime = 8000] 

 F3 : ProcFunction
  [execTime = 5000]     
   
 localFPGAFunctions -> ProcFunction 4..4
 [localFPGAFunctions = B1,C1,E1,D1]
 
 localGPUFunctions -> ProcFunction 2..2
 [localGPUFunctions = C20,C21] 
 
 localGPPFunctions -> ProcFunction 11..11
 [localGPPFunctions = C30,C31,C4,E2,D2,F1,C5,F2,F3,A1,A2]

abstract HardwareArchitecture
 board0 : Board
  [no fpga] 
 board1 : Board
  [no fpga]
 board2 : Board
  [fpga]
 board3 : Board 
  [fpga]
 
Architecture 
 myArchitecture : HardwareArchitecture 
 mydataFlowArchitecture : SubsystemFunctionalArchitecture 

